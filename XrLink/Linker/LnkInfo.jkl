//
// Implementation of linker info dumping commands.
// Dumps more info than me at 3 AM after eight Diet Dr Peppers.
//

#INCLUDE "<inc>/Linker.hjk"

LnkArchitectureCodeToName : ^UBYTE[XLO_ARCHITECTURE_MAX] = {
    [XLO_ARCHITECTURE_XR17032] = "xr17032",
    [XLO_ARCHITECTURE_FOX32] = "fox32",
    [XLO_ARCHITECTURE_APHELION] = "aphelion",
    [XLO_ARCHITECTURE_AMD64] = "amd64",
}

LnkFlagToName : ^UBYTE[32] = {
    [0] = "FRAGMENT",
}

LnkSectionFlagToName : ^UBYTE[32] = {
    [0] = "ZERO",
    [1] = "CODE",
    [2] = "MAP",
}

FN LnkDumpInfo (
    IN filename : ^UBYTE,
)

    buf := LnkLoadXloFile ( filename )

    header := CAST buf TO ^XloHeader

    archname := "Unknown"

    IF header^.TargetArchitecture < XLO_ARCHITECTURE_MAX AND
        LnkArchitectureCodeToName[header^.TargetArchitecture] THEN

        archname = LnkArchitectureCodeToName[header^.TargetArchitecture]
    END

    TlPrint ( "DateStamp     %08X\n", header^.Timestamp )
    TlPrint ( "Architecture  %s\n", archname )
    TlPrint ( "Head Length   %u\n", header^.HeadLength )
    TlPrint ( "Flags         " )

    flags := header^.Flags
    i := 0
    printed := FALSE

    WHILE flags AND i < 32 DO
        IF flags & 1 THEN
            IF printed THEN
                TlPrint ( " | " )
            END

            IF LnkFlagToName[i] THEN
                TlPrint ( LnkFlagToName[i] )
            
            ELSE
                TlPrint ( "FLAG%d", i )
            END

            printed = TRUE
        END

        i += 1
        flags >>= 1
    END

    TlPrint ( "\n\nSections:\n" )

    i = 0
    section := CAST buf + header^.SectionTableOffset TO ^XloSectionHeader
    stringtab := buf + header^.StringTableOffset

    WHILE i < header^.SectionCount DO
        TlPrint ( "%s:\n", &stringtab[section^.NameOffset] )
        TlPrint ( "  Size     %u bytes\n", section^.DataLength)
        TlPrint ( "  Address  0x%P\n", section^.VirtualAddress )
        TlPrint ( "  Offset   0x%08X\n", section^.FileOffset )
        TlPrint ( "  Flags    " )

        j := 0
        printed = FALSE
        flags = section^.Flags

        WHILE flags AND j < 32 DO
            IF flags & 1 THEN
                IF printed THEN
                    TlPrint ( " | " )
                END

                IF LnkSectionFlagToName[j] THEN
                    TlPrint ( LnkSectionFlagToName[j] )
                
                ELSE
                    TlPrint ( "FLAG%d", j )
                END

                printed = TRUE
            END

            j += 1
            flags >>= 1
        END

        TlPrint ( "\n\n" )

        section += SIZEOF XloSectionHeader
        i += 1
    END
END

FN LnkDumpSymbols (
    IN filename : ^UBYTE,
)

    buf := LnkLoadXloFile ( filename )

    header := CAST buf TO ^XloHeader

    i := 0
    symbol := CAST buf + header^.SymbolTableOffset TO ^XloSymbolEntry
    stringtab := buf + header^.StringTableOffset
    sectiontab := CAST buf + header^.SectionTableOffset TO ^XloSectionHeader

    WHILE i < header^.SymbolCount DO
        IF symbol^.Type == XLO_SYMBOL_GLOBAL THEN
            TlPrint ( "G " )

        ELSEIF symbol^.Type == XLO_SYMBOL_EXPORT THEN
            TlPrint ( "E ")
        
        ELSE
            TlPrint ( "? " )
        END

        section := &sectiontab[symbol^.SectionIndex]

        TlPrint ( "%08X %8s %s\n",
            symbol^.SectionOffset,
            stringtab + section^.NameOffset,
            stringtab + symbol^.NameOffset )

        symbol += SIZEOF XloSymbolEntry
        i += 1
    END
END

FN LnkDumpExterns (
    IN filename : ^UBYTE,
)

    buf := LnkLoadXloFile ( filename )

    header := CAST buf TO ^XloHeader

    i := 0
    extern := CAST buf + header^.ExternTableOffset TO ^XloExternEntry
    stringtab := buf + header^.StringTableOffset

    WHILE i < header^.ExternCount DO
        TlPrint ( "%4d %s\n", i, stringtab + extern^.NameOffset )

        extern += SIZEOF XloExternEntry
        i += 1
    END
END

XloRelocTypeNames : ^UBYTE[XLO_RELOC_MAX] = {
    [XLO_RELOC_PTR] = "PTR",

    [XLO_RELOC_XR17032_ABSJ] = "XR17032_ABSJ",
    [XLO_RELOC_XR17032_LA] = "XR17032_LA",
    [XLO_RELOC_XR17032_FAR_INT] = "XR17032_FAR_INT",
    [XLO_RELOC_XR17032_FAR_LONG] = "XR17032_FAR_LONG",

    [XLO_RELOC_FOX32_CALL] = "FOX32_CALL",
}

FN LnkDumpRelocs (
    IN filename : ^UBYTE,
)

    buf := LnkLoadXloFile ( filename )

    header := CAST buf TO ^XloHeader

    i := 0
    sectiontab := CAST buf + header^.SectionTableOffset TO ^XloSectionHeader
    stringtab := buf + header^.StringTableOffset
    externtab := CAST buf + header^.ExternTableOffset TO ^XloExternEntry

    j : UWORD
    reloc : ^XloRelocEntry

    IF header^.Flags & XLO_FILE_FRAGMENT THEN
        extheader := CAST buf TO ^XloHeaderExtended

        TlPrint ( "EXTERN:\n" )

        j = 0
        reloc =
            CAST buf + extheader^.UnresolvedFixupTableOffset TO ^XloRelocEntry

        WHILE j < extheader^.UnresolvedFixupCount DO
            relocname := "UnknownType"

            IF reloc^.Type < XLO_RELOC_MAX AND
                XloRelocTypeNames[reloc^.Type] THEN

                relocname = XloRelocTypeNames[reloc^.Type]
            END

            TlPrint ( "%8s @ %08X %s of '%s'\n",
                stringtab + sectiontab[reloc^.SectionIndex].NameOffset,
                reloc^.SectionOffset,
                relocname,
                stringtab + externtab[reloc^.ExternIndex].NameOffset )

            reloc += SIZEOF XloRelocEntry
            j += 1
        END

        TlPrint ( "\n" )
    END

    WHILE i < header^.SectionCount DO
        TlPrint ( "%s:\n", stringtab + sectiontab[i].NameOffset )

        j = 0
        reloc = CAST buf + sectiontab[i].RelocTableOffset TO ^XloRelocEntry

        WHILE j < sectiontab[i].RelocCount DO
            relocname := "UnknownType"

            IF reloc^.Type < XLO_RELOC_MAX AND
                XloRelocTypeNames[reloc^.Type] THEN

                relocname = XloRelocTypeNames[reloc^.Type]
            END

            TlPrint ( "%8s @ %08X %s\n",
                stringtab + sectiontab[reloc^.SectionIndex].NameOffset,
                reloc^.SectionOffset,
                relocname )

            reloc += SIZEOF XloRelocEntry
            j += 1
        END

        TlPrint ( "\n" )

        i += 1
    END
END

FN LnkDumpFixups (
    IN filename : ^UBYTE,
)

    TlInternalError ( "TODO fixups dumping deferred until there are object files with fixups", 0, 0, 0 )
END

FN LnkDumpImports (
    IN filename : ^UBYTE,
)

    TlInternalError ( "TODO imports dumping deferred until there are object files with imports", 0, 0, 0 )
END