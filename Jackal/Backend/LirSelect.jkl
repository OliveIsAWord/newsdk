//
// Implements the backbone of instruction selection.
//

#INCLUDE "<inc>/Target.hjk"

PUBLIC LirCurrentFunction : ^IrFunction

FN LirCreateInstruction (
    IN type : LirInstructionType,
    IN machtype : UBYTE,
) : ^LirInstruction

    lirinst : ^LirInstruction

    status := TlBumpAlloc (
        SIZEOF LirInstruction, // bytes
        OUT lirinst, // ptr
    )

    IF status THEN
        TlInternalError ( "Failed to create LIR instruction", 0, 0, 0 )
    END

    lirinst^.Type = type
    lirinst^.Defines = NULLPTR
    lirinst^.Source1 = NULLPTR
    lirinst^.Source2 = NULLPTR
    lirinst^.MachineInstruction = machtype

    RETURN lirinst
END

FN LirCreateRegister () : ^LirRegister

    reg : ^LirRegister

    status := TlBumpAlloc (
        SIZEOF LirRegister, // bytes
        OUT reg, // ptr
    )

    IF status THEN
        TlInternalError ( "Failed to create LIR register", 0, 0, 0 )
    END

    reg^.Type = LIR_VIRTUAL_REG

    RETURN reg
END

FN LirInsertInstructionTail (
    IN lirinst : ^LirInstruction,
)

    // Insert the instruction at the end of the current chain.

    TlPrintNumber ( lirinst^.Type )
    TlPrintString ( "\n" )
    TlPrintNumber ( lirinst^.MachineInstruction )
    TlPrintString ( "\n" )

    lirinst^.Block = LirCurrentFunction
    lirinst^.Next = NULLPTR
    lirinst^.Prev = LirCurrentFunction^.LirTail

    IF NOT LirCurrentFunction^.LirTail THEN
        LirCurrentFunction^.LirHead = lirinst
    ELSE
        LirCurrentFunction^.LirTail^.Next = lirinst
    END

    LirCurrentFunction^.LirTail = lirinst
END

FN LirRemoveInstruction (
    IN lirinst : ^LirInstruction,
)

    // Remove the instruction from the chain.

    block := lirinst^.Block

    p := lirinst^.Prev

    IF p THEN
        p^.Next = lirinst^.Next
    ELSE // no prev means we were the head
        block^.LirHead = lirinst^.Next
    END

    p = lirinst^.Next

    IF p THEN
        p^.Prev = lirinst^.Prev
    ELSE // no next means we were the tail
        block^.LirTail = lirinst^.Prev
    END
END

FN LirGetLabel (
    IN oldlabel : ^IrInstruction,
) : ^LirInstruction

    // Get the LIR version of an IR label.

    lirinst := oldlabel^.Body.Label.LirLabel

    IF lirinst THEN
        lirinst^.Body.Label.References += 1

        RETURN lirinst
    END

    lirinst = LirCreateInstruction (
        LIR_LABEL, // type
        0, // machtype
    )

    oldlabel^.Body.Label.LirLabel = lirinst

    lirinst^.Body.Label.References = 1

    RETURN lirinst
END

FN LirGetRegister (
    IN oldvariable : ^IrVariable,
) : ^LirRegister

    // Get the LIR version of an IR variable.

    lirreg := oldvariable^.LirRegister

    IF lirreg THEN
        RETURN lirreg
    END

    lirreg = LirCreateRegister ()

    oldvariable^.LirRegister = lirreg

    RETURN lirreg
END

FN LirSelect (
    IN inst : ^IrInstruction,
) : ^LirRegister

    reg := inst^.LirRegister

    IF reg THEN
        RETURN reg
    END

    reg = JklTargetInfo^.LirInfo^.SelectionTable[inst^.Type] (
        inst, // inst
    )

    IF reg AND reg^.Type == LIR_MACHINE_REG THEN
        // The selected instruction shovels its results into a fixed register.
        // Create a move instruction whose destination is a virtual register,
        // with a hint that regalloc should assign it to that machine register.
        // If regalloc succeeds, the move instruction will be elided.

        #IF 0
            machreg := reg^.MachineRegister

            reg = JklTargetInfo^.LirInfo^.MoveFunc (
                reg, // oldregister
            )

            reg^.Hint = machreg

        #ELSE
            TlInternalError ( "Machine -> Virt TODO", 0, 0, 0 )
        #END
    END

    inst^.LirRegister = reg

    RETURN reg
END

FN LirSelectOrGetRegister (
    IN var : ^IrVariable,
) : ^LirRegister

    // Get the contents of the variable in the form of a LIR register.

    IF var^.DefinedBy THEN
        RETURN LirSelect ( var^.DefinedBy )
    END

    RETURN LirGetRegister ( var )
END

FN LirSelectForFunction (
    IN funcsym : ^LexSymbol,
)

    // Call the target's selection function for each instruction in this
    // function.

    irfunc := CAST funcsym^.InitialValue TO ^IrFunction

    irfunc^.LirHead = NULLPTR
    irfunc^.LirTail = NULLPTR

    inst := irfunc^.Head

    LirCurrentFunction = irfunc

    WHILE inst DO
        // If this IR instruction has a side effect we care about, then call the
        // target select function to select over the basic block DAG. These are:
        //
        //  o Function calls
        //  o Memory stores
        //  o Labels
        //  o Branches
        //  o Moves to named locals

        IF inst^.Type == IR_LABEL THEN
            // Just drop in a label, don't bother the target.

            lirinst := inst^.Body.Label.LirLabel

            IF NOT lirinst THEN
                lirinst = LirCreateInstruction (
                    LIR_LABEL, // type
                    0, // machtype
                )

                inst^.Body.Label.LirLabel = lirinst

                lirinst^.Body.Label.References = 0
            END

            LirInsertInstructionTail ( lirinst )

        ELSEIF inst^.Type == IR_CALL OR
            inst^.Type == IR_STORE OR
            (inst^.Type == IR_MOVE AND inst^.Defines^.Symbol) OR
            (inst^.Type > IR_BRANCH_LOW_NUMBER AND
            inst^.Type < IR_BRANCH_HIGH_NUMBER) THEN

            LirSelect ( inst )
        END

        inst = inst^.Next
    END
END