//
// Implements support routines for value numbering.
//

#INCLUDE "Be.hjk"

#DEFINE VN_HASH_SIZE 97

STRUCT IrHashTableRecord
    OrderedListHead : TlListEntry,
    BucketHeads : TlListEntry[VN_HASH_SIZE],
END

IrHashTable : IrHashTableRecord

IrNextValueNumber : ULONG = 1

FN IrValueNumberBarrier (
    IN onlyloads : UWORD,
    IN symbol : ^LexSymbol,
)

    // Cause a value number barrier; clear the hash table.

    head := &IrHashTable.OrderedListHead
    listentry := head^.Next

    WHILE listentry != head DO
        nextentry := listentry^.Next

        var := CONTAINEROF listentry TO IrVariable.Opt.Vn.ListEntry

        IF NOT onlyloads OR var^.Opt.Vn.AcquiredByLoad THEN
            IF NOT symbol OR
                var^.Opt.Vn.AcquiredByLoadOf == symbol OR
                NOT var^.Opt.Vn.AcquiredByLoadOf THEN

                var^.Opt.Vn.ValueNumber = 0

                TlRemoveEntryList ( &var^.Opt.Vn.ListEntry )
                TlRemoveEntryList ( &var^.Opt.Vn.HashEntry )
            END
        END

        listentry = nextentry
    END
END

FN IrKillValueNumber (
    IN value : ULONG,
)

    // Kill all expressions that use the given value number.

    head := &IrHashTable.OrderedListHead
    listentry := head^.Next

    WHILE listentry != head DO
        var := CONTAINEROF listentry TO IrVariable.Opt.Vn.ListEntry

        nextentry := listentry^.Next

        IF var^.Opt.Vn.KeySrc1 == value OR var^.Opt.Vn.KeySrc2 == value THEN
            var^.Opt.Vn.ValueNumber = 0

            TlRemoveEntryList ( &var^.Opt.Vn.ListEntry )
            TlRemoveEntryList ( &var^.Opt.Vn.HashEntry )
        END

        listentry = nextentry
    END
END

FN IrKillVariable (
    IN var : ^IrVariable,
)

    IrKillValueNumber ( var^.Opt.Vn.ValueNumber )

    IF var^.Opt.Vn.KeyOp THEN
        TlRemoveEntryList ( &var^.Opt.Vn.ListEntry )
        TlRemoveEntryList ( &var^.Opt.Vn.HashEntry )
    END
END

FN IrGiveOpaqueValueVariable (
    IN var : ^IrVariable,
)

    IF var^.Opt.Vn.KeyOp THEN
        // This variable already has a value number, so remove it from its place
        // in the value number hash table.

        TlRemoveEntryList ( &var^.Opt.Vn.HashEntry )
        TlRemoveEntryList ( &var^.Opt.Vn.ListEntry )

        // Also kill any values that used this variable's old value number.

        IrKillValueNumber ( var^.Opt.Vn.ValueNumber )
    END

    var^.Opt.Vn.KeyOp = 0

    var^.Opt.Vn.ValueNumber = IrNextValueNumber
    IrNextValueNumber += 1
END

FN IrValueNumberVariable (
    IN var : ^IrVariable,
    IN valueop : IrInstructionType,
    IN src1 : ^IrVariable,
    IN src2 : ^IrVariable,
)

    // Give the variable a (new) value number.

    byload := var^.Opt.Vn.AcquiredByLoad | src1^.Opt.Vn.AcquiredByLoad

    byloadof := var^.Opt.Vn.AcquiredByLoadOf

    IF NOT byloadof THEN
        byloadof = src1^.Opt.Vn.AcquiredByLoadOf

    ELSEIF src1^.Opt.Vn.AcquiredByLoadOf THEN
        byloadof = NULLPTR
    END

    valuesrc1 := src1^.Opt.Vn.ValueNumber

    IF NOT valuesrc1 THEN
        LEAVE
    END

    valuesrc2 : ULONG = 0

    IF src2 THEN
        valuesrc2 = src2^.Opt.Vn.ValueNumber
        byload |= src2^.Opt.Vn.AcquiredByLoad

        IF NOT byloadof THEN
            byloadof = src2^.Opt.Vn.AcquiredByLoadOf

        ELSEIF src2^.Opt.Vn.AcquiredByLoadOf THEN
            byloadof = NULLPTR
        END

        IF NOT valuesrc2 THEN
            LEAVE
        END
    END

    IF var^.Opt.Vn.KeyOp THEN
        // This variable already has a value number, so remove it from its place
        // in the value number hash table.

        TlRemoveEntryList ( &var^.Opt.Vn.HashEntry )

        // Also kill any values that used this variable's old value number.

        IrKillValueNumber ( var^.Opt.Vn.ValueNumber )
    ELSE
        // Put the variable at the head of the global list.

        TlInsertAtHeadList (
            &IrHashTable.OrderedListHead, // head
            &var^.Opt.Vn.ListEntry, // entry
        )
    END

    vn := IrNextValueNumber
    IrNextValueNumber += 1

    var^.Opt.Vn.ValueNumber = vn

    var^.Opt.Vn.AcquiredByLoad = byload
    var^.Opt.Vn.AcquiredByLoadOf = byloadof

    var^.Opt.Vn.KeySrc1 = valuesrc1
    var^.Opt.Vn.KeySrc2 = valuesrc2
    var^.Opt.Vn.KeyOp = valueop

    hash : ULONG = valuesrc1 $ valuesrc2 $ valueop
    hash $= hash >> 7
    hash $= hash >> 14
    hash $= hash >> 21
    hash $= hash >> 28
    hash %= VN_HASH_SIZE

    TlInsertAtHeadList (
        &IrHashTable.BucketHeads[hash], // head
        &var^.Opt.Vn.HashEntry,
    )
END

FN IrValueNumberConstantVariable (
    IN var : ^IrVariable,
    IN constant : UWORD,
    IN op : IrInstructionType,
)

    // Give the variable a (new) value number.

    IF var^.Opt.Vn.KeyOp THEN
        // This variable already has a value number, so remove it from its place
        // in the value number hash table.

        TlRemoveEntryList ( &var^.Opt.Vn.HashEntry )

        // Also kill any values that used this variable's old value number.

        IrKillValueNumber ( var^.Opt.Vn.ValueNumber )
    ELSE
        // Put the variable at the head of the global list.

        TlInsertAtHeadList (
            &IrHashTable.OrderedListHead, // head
            &var^.Opt.Vn.ListEntry, // entry
        )
    END

    vn := IrNextValueNumber
    IrNextValueNumber += 1

    var^.Opt.Vn.ValueNumber = vn

    var^.Opt.Vn.ConstantValue = constant
    var^.Opt.Vn.KeyOp = op

    hash : ULONG = constant
    hash $= hash >> 7
    hash $= hash >> 14
    hash $= hash >> 21
    hash $= hash >> 28
    hash %= VN_HASH_SIZE

    TlInsertAtHeadList (
        &IrHashTable.BucketHeads[hash], // head
        &var^.Opt.Vn.HashEntry,
    )
END

FN IrFindValue (
    IN op : IrInstructionType,
    IN src1 : ^IrVariable,
    IN src2 : ^IrVariable,
) : ^IrVariable

    // Find a value matching the source variables and opcode provided.

    valuesrc1 : ULONG = 0
    valuesrc2 : ULONG = 0

    IF src1 THEN
        valuesrc1 = src1^.Opt.Vn.ValueNumber
    END

    IF src2 THEN
        valuesrc2 = src2^.Opt.Vn.ValueNumber
    END

    hash : ULONG = valuesrc1 $ valuesrc2 $ op
    hash $= hash >> 7
    hash $= hash >> 14
    hash $= hash >> 21
    hash $= hash >> 28
    hash %= VN_HASH_SIZE

    head := &IrHashTable.BucketHeads[hash]
    listentry := head^.Next

    WHILE listentry != head DO
        var := CONTAINEROF listentry TO IrVariable.Opt.Vn.HashEntry

        IF var^.Opt.Vn.KeyOp == op THEN
            IF var^.Opt.Vn.KeySrc1 == valuesrc1 AND
                var^.Opt.Vn.KeySrc2 == valuesrc2 THEN

                var^.Annotation = IR_VAR_UNINITIALIZED

                RETURN var

            ELSEIF IrCommutativity[op] AND
                var^.Opt.Vn.KeySrc1 == valuesrc2 AND
                var^.Opt.Vn.KeySrc2 == valuesrc1 THEN

                var^.Annotation = IR_VAR_UNINITIALIZED

                RETURN var
            END
        END

        listentry = listentry^.Next
    END

    RETURN NULLPTR
END

FN IrFindConstant (
    IN constant : UWORD,
    IN op : IrInstructionType,
) : ^IrVariable

    // Find a value matching the source variables and opcode provided.

    hash : ULONG = constant
    hash $= hash >> 7
    hash $= hash >> 14
    hash $= hash >> 21
    hash $= hash >> 28
    hash %= VN_HASH_SIZE

    head := &IrHashTable.BucketHeads[hash]
    listentry := head^.Next

    WHILE listentry != head DO
        var := CONTAINEROF listentry TO IrVariable.Opt.Vn.HashEntry

        IF var^.Opt.Vn.KeyOp == op AND
            var^.Opt.Vn.ConstantValue == constant THEN

            var^.Annotation = IR_VAR_UNINITIALIZED

            RETURN var
        END

        listentry = listentry^.Next
    END

    RETURN NULLPTR
END

FN IrInitializeValueNumber ()

    // Initialize the structures for value numbering.

    TlInitializeList ( &IrHashTable.OrderedListHead )

    i := 0

    WHILE i < VN_HASH_SIZE DO
        TlInitializeList ( &IrHashTable.BucketHeads[i] )

        i += 1
    END
END