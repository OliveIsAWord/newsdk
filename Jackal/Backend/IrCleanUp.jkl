//
// IR clean-up peephole pass.
//

#INCLUDE "Be.hjk"

IrResolveGenCount : ULONG = 0

FN IrResolveLabel (
    IN label : ^IrInstruction,
    OUT newlabel : ^IrInstruction,
) : UWORD

    // Follow any chain of unconditional branches to get the "real" label.

    resolved := FALSE
    newlabel = label

    IrResolveGenCount += 1
    curgen := IrResolveGenCount

    WHILE TRUE DO
        // First slide to the next non-label instruction.

        next := label

        WHILE TRUE DO
            next = next^.Next

            IF NOT next OR next^.Type != IR_LABEL THEN
                BREAK
            END

            label = next
            newlabel = next

            resolved = TRUE
        END

        IF NOT next THEN
            RETURN resolved
        END

        // If this instruction is an unconditional jump, follow it.

        IF next^.Type == IR_JUMP THEN
            label = next^.Body.Jump.Label
            newlabel = label
            resolved = TRUE

            IF label^.Body.Label.GenCount == curgen THEN
                // We went in a cycle. Stop now.

                RETURN resolved
            END

            label^.Body.Label.GenCount = curgen

        ELSE
            RETURN resolved
        END
    END
END

FN IrCleanUpForFunction (
    IN funcsym : ^LexSymbol,
) : UWORD

    // Clean up the IR for a function in a single pass.

    irfunc := CAST funcsym^.InitialValue TO ^IrFunction

    dead := FALSE
    again := FALSE
    cleaned := FALSE

    inst := irfunc^.Head

    WHILE inst DO
        IF inst^.Type == IR_LABEL THEN
            dead = FALSE

        ELSEIF dead THEN
            // Remove dead instruction.

            IrRemoveInstruction ( inst )

            cleaned = TRUE

        ELSEIF inst^.Type == IR_JUMP THEN
            dead = TRUE

            IrResolveLabel (
                inst^.Body.Jump.Label, // label
                OUT inst^.Body.Jump.Label, // newlabel
            )

            next := inst^.Next

            WHILE next AND next^.Type == IR_LABEL DO
                IF inst^.Body.Jump.Label == next THEN
                    IrRemoveInstruction ( inst )

                    cleaned = TRUE

                    BREAK
                END

                next = next^.Next
            END

        ELSEIF (inst^.Type > IR_BINARY_BRANCH_LOW_NUMBER AND
            inst^.Type < IR_BINARY_BRANCH_HIGH_NUMBER) THEN

            IrResolveLabel (
                inst^.Body.Branch.TrueLabel, // label
                OUT inst^.Body.Branch.TrueLabel, // newlabel
            )

            IrResolveLabel (
                inst^.Body.Branch.FalseLabel, // label
                OUT inst^.Body.Branch.FalseLabel, // newlabel
            )

            IF inst^.Body.Branch.TrueLabel == inst^.Body.Branch.FalseLabel THEN
                // Turn into an unconditional jump.

                unilabel := inst^.Body.Branch.TrueLabel

                IrSimplifyToJump ( inst )

                inst^.Body.Jump.Label = unilabel

                // Step back one instruction to cause this new unconditional jump
                // to be processed.

                again = TRUE
            END
        END

        IF NOT again THEN
            inst = inst^.Next
        END

        again = FALSE
    END

    RETURN cleaned
END

FN IrCleanUp ()

    // For each function definition, clean up IR.

    funcsym := PrsFunctionListHead

    WHILE funcsym DO
        IrCleanUpForFunction ( funcsym )

        funcsym = funcsym^.NextFunction
    END
END