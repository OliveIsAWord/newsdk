//
// Lexer.
//

#INCLUDE "<inc>/Lexer.jh"

// The "engine" of the lexer, the bit that provides the character stream,
// operates by stacking and unstacking source text streams. Each stream has a
// filename and a current line number associated with it, which are ultimately
// used to generate useful diagnostic messages. Streams are associated with
// include files, and also with preprocessor macro expansion.

LexCharTreatment : LexCharBehavior[256] = {
    [0] = CHAR_EOF,

    [' ']  = CHAR_WHITESPACE,
    ['\n'] = CHAR_WHITESPACE,
    ['\t'] = CHAR_WHITESPACE,

    ['^'] = CHAR_SPLIT,
    ['('] = CHAR_SPLIT,
    [')'] = CHAR_SPLIT,
    ['~'] = CHAR_SPLIT,
    [','] = CHAR_SPLIT,
    ['['] = CHAR_SPLIT,
    [']'] = CHAR_SPLIT,
    [':'] = CHAR_SPLIT,
    ['{'] = CHAR_SPLIT,
    ['}'] = CHAR_SPLIT,
    ['#'] = CHAR_SPLIT,

    ['='] = CHAR_COALESCE,
    ['&'] = CHAR_COALESCE,
    ['|'] = CHAR_COALESCE,
    ['!'] = CHAR_COALESCE,
    ['<'] = CHAR_COALESCE,
    ['>'] = CHAR_COALESCE,
    ['+'] = CHAR_COALESCE,
    ['-'] = CHAR_COALESCE,
    ['*'] = CHAR_COALESCE,
    ['/'] = CHAR_COALESCE,
    ['%'] = CHAR_COALESCE,
    ['.'] = CHAR_COALESCE,
    ['@'] = CHAR_COALESCE,
    ['$'] = CHAR_COALESCE,

    // Since CHAR_NORMAL is the first field in the enum, it is guaranteed to
    // be zero in value, so we don't have to explicitly set all the other
    // character behaviors to it, since any non-defined fields here will also
    // be zero.
}

CONST LEX_TOKEN_STRING_SIZE := 2048

LexPutbackStack : LexToken[LEX_PUTBACK_STACK_DEPTH]

LexCurrentStream : ^LexStream = NULLPTR
LexCurrentMacroScope : ^TlSymbolTable = NULLPTR
LexFalseCount : ULONG = 0
LexPutbackStackPtr : ULONG = 0

LexCurrentScope : ^TlSymbolTable = NULLPTR

STRUCT LexKeyword
    Entry : TlHashTableEntry, // MUST be at the beginning
    Type : LexTokenType,
    Subtype : LexTokenSubtype,
END

LexKeywordHashTable : TlHashTable
LexInternedStringHashTable : TlHashTable

LexKeywordBumpArray : LexKeyword[128]
LexKeywordBumpIndex := 0

FN LexKeywordInsert (
    IN name : ^UBYTE,
    IN type : LexTokenType,
    IN subtype : LexTokenSubtype,
)

    // Insert the keyword into the keyword hash table.
    // This is done every time the compiler is invoked so it should be VERY
    // fast. To this end we completely avoid dynamic allocation with this silly
    // keyword structure bump allocator. The hash table package does no dynamic
    // allocation, nor does it do any string copies (though it does iterate the
    // string to calculate the hash), so this should all be quite speedy.

    keyword := &LexKeywordBumpArray[LexKeywordBumpIndex]
    LexKeywordBumpIndex += 1

    keyword^.Type = type
    keyword^.Subtype = subtype

    TlInsertHashTable (
        &LexKeywordHashTable, // hashtable
        &keyword^.Entry, // entry
        name, // key
    )
END

FN LexInitialize ()

    dumpster : UBYTE

    // Create the file block for the source file.

    fileblock := FeCreateFileBlock (
        &FeInputFile[0], // filepath
        "__SRC__", // includename
        &dumpster, // OUT created
    )

    // Create the root file stream.

    filestream := LexCreateFileStream (
        fileblock, // fileblock
        FeInputFileHandle, // handle
    )

    LexPushStream ( filestream )

    // Create the root macro scope.

    LexCurrentMacroScope = TlCreateSymbolTable (
        0, // outerscope
        &LexMacroDelete, // deletefunc
    )

    // Create the global symbol table scope.

    LexCurrentScope = TlCreateSymbolTable (
        0, // outerscope
        &LexSymbolDelete, // deletefunc
    )

    // Initialize interned string hash table.

    TlInitializeHashTable ( &LexInternedStringHashTable )

    // Initialize keyword hash table.

    TlInitializeHashTable ( &LexKeywordHashTable )

    LexKeywordInsert ( "AND", TOKEN_OPER, TOKEN_AND )
    LexKeywordInsert ( "BEGIN", TOKEN_STATEMENT, TOKEN_BEGIN )
    LexKeywordInsert ( "BREAK", TOKEN_STATEMENT, TOKEN_BREAK )
    LexKeywordInsert ( "BYTE", TOKEN_PTYPE, TOKEN_BYTE )
    LexKeywordInsert ( "CAST", TOKEN_OPER, TOKEN_CAST )
    LexKeywordInsert ( "CONST", TOKEN_DECL, TOKEN_CONST )
    LexKeywordInsert ( "CONTINUE", TOKEN_STATEMENT, TOKEN_CONTINUE )
    LexKeywordInsert ( "DO", TOKEN_OTHER, TOKEN_DO )
    LexKeywordInsert ( "ELSE", TOKEN_STATEMENT, TOKEN_ELSE )
    LexKeywordInsert ( "ELSEIF", TOKEN_STATEMENT, TOKEN_ELSEIF )
    LexKeywordInsert ( "END", TOKEN_STATEMENT, TOKEN_END )
    LexKeywordInsert ( "ENUM", TOKEN_DECL, TOKEN_ENUM )
    LexKeywordInsert ( "EXTERN", TOKEN_DECL, TOKEN_EXTERN )
    LexKeywordInsert ( "FALSE", TOKEN_VALUE, TOKEN_FALSE )
    LexKeywordInsert ( "FN", TOKEN_DECL, TOKEN_FN )
    LexKeywordInsert ( "FNPTR", TOKEN_DECL, TOKEN_FNPTR )
    LexKeywordInsert ( "FNSECTION", TOKEN_STATEMENT, TOKEN_FNSECTION )
    LexKeywordInsert ( "GOTO", TOKEN_STATEMENT, TOKEN_GOTO )
    LexKeywordInsert ( "IF", TOKEN_STATEMENT, TOKEN_IF )
    LexKeywordInsert ( "IN", TOKEN_ARGSPEC, TOKEN_IN )
    LexKeywordInsert ( "INT", TOKEN_PTYPE, TOKEN_INT )
    LexKeywordInsert ( "LEAVE", TOKEN_STATEMENT, TOKEN_LEAVE )
    LexKeywordInsert ( "LONG", TOKEN_PTYPE, TOKEN_LONG )
    LexKeywordInsert ( "NOT", TOKEN_OPER, TOKEN_NOT )
    LexKeywordInsert ( "NULL", TOKEN_VALUE, TOKEN_NULL )
    LexKeywordInsert ( "NULLPTR", TOKEN_VALUE, TOKEN_NULLPTR )
    LexKeywordInsert ( "OR", TOKEN_OPER, TOKEN_OR )
    LexKeywordInsert ( "OUT", TOKEN_ARGSPEC, TOKEN_OUT )
    LexKeywordInsert ( "PACKED", TOKEN_OTHER, TOKEN_PACKED )
    LexKeywordInsert ( "PUBLIC", TOKEN_DECL, TOKEN_PUBLIC )
    LexKeywordInsert ( "RETURN", TOKEN_STATEMENT, TOKEN_RETURN )
    LexKeywordInsert ( "SECTION", TOKEN_DECL, TOKEN_SECTION )
    LexKeywordInsert ( "SIZEOF", TOKEN_VALUE, TOKEN_SIZEOF )
    LexKeywordInsert ( "SIZEOFVALUE", TOKEN_OPER, TOKEN_SIZEOFVALUE )
    LexKeywordInsert ( "STRUCT", TOKEN_DECL, TOKEN_STRUCT )
    LexKeywordInsert ( "THEN", TOKEN_OTHER, TOKEN_THEN )
    LexKeywordInsert ( "TO", TOKEN_OTHER, TOKEN_TO )
    LexKeywordInsert ( "TRUE", TOKEN_VALUE, TOKEN_TRUE )
    LexKeywordInsert ( "TYPE", TOKEN_DECL, TOKEN_TYPE )
    LexKeywordInsert ( "UBYTE", TOKEN_PTYPE, TOKEN_UBYTE )
    LexKeywordInsert ( "UINT", TOKEN_PTYPE, TOKEN_UINT )
    LexKeywordInsert ( "ULONG", TOKEN_PTYPE, TOKEN_ULONG )
    LexKeywordInsert ( "UNION", TOKEN_DECL, TOKEN_UNION )
    LexKeywordInsert ( "VOID", TOKEN_PTYPE, TOKEN_VOID )
    LexKeywordInsert ( "WHILE", TOKEN_STATEMENT, TOKEN_WHILE )

    LexKeywordInsert ( "(", TOKEN_LPAREN, NULL )
    LexKeywordInsert ( ")", TOKEN_RPAREN, NULL )
    LexKeywordInsert ( "[", TOKEN_LBRACKET, NULL )
    LexKeywordInsert ( "]", TOKEN_RBRACKET, NULL )
    LexKeywordInsert ( "^", TOKEN_CARET, NULL )
    LexKeywordInsert ( ":", TOKEN_COLON, NULL )
    LexKeywordInsert ( "{", TOKEN_LBRACE, NULL )
    LexKeywordInsert ( "}", TOKEN_RBRACE, NULL )
    LexKeywordInsert ( ",", TOKEN_COMMA, NULL )

    LexKeywordInsert ( "==", TOKEN_OPER, TOKEN_EQUIV )
    LexKeywordInsert ( "!=", TOKEN_OPER, TOKEN_NOTEQUIV )
    LexKeywordInsert ( "&", TOKEN_OPER, TOKEN_BITAND )
    LexKeywordInsert ( "|", TOKEN_OPER, TOKEN_BITOR )
    LexKeywordInsert ( "<", TOKEN_OPER, TOKEN_LESSTHAN )
    LexKeywordInsert ( ">", TOKEN_OPER, TOKEN_GREATERTHAN )
    LexKeywordInsert ( "<=", TOKEN_OPER, TOKEN_LTEQ )
    LexKeywordInsert ( ">=", TOKEN_OPER, TOKEN_GTEQ )
    LexKeywordInsert ( "+", TOKEN_OPER, TOKEN_PLUS )
    LexKeywordInsert ( "-", TOKEN_OPER, TOKEN_MINUS )
    LexKeywordInsert ( "/", TOKEN_OPER, TOKEN_DIVIDE )
    LexKeywordInsert ( "%", TOKEN_OPER, TOKEN_MODULO )
    LexKeywordInsert ( ".", TOKEN_OPER, TOKEN_DOT )
    LexKeywordInsert ( "@", TOKEN_STATEMENT, TOKEN_LABEL )
    LexKeywordInsert ( "$", TOKEN_OPER, TOKEN_BITXOR )
    LexKeywordInsert ( "<<", TOKEN_OPER, TOKEN_LEFTSHIFT )
    LexKeywordInsert ( ">>", TOKEN_OPER, TOKEN_RIGHTSHIFT )
    LexKeywordInsert ( "~", TOKEN_OPER, TOKEN_BITNOT )

    LexKeywordInsert ( "=", TOKEN_ASSIGN, TOKEN_EQUALS )
    LexKeywordInsert ( "+=", TOKEN_ASSIGN, TOKEN_PLUSEQUALS )
    LexKeywordInsert ( "-=", TOKEN_ASSIGN, TOKEN_MINUSEQUALS )
    LexKeywordInsert ( "*=", TOKEN_ASSIGN, TOKEN_MULEQUALS )
    LexKeywordInsert ( "/=", TOKEN_ASSIGN, TOKEN_DIVEQUALS )
    LexKeywordInsert ( "%=", TOKEN_ASSIGN, TOKEN_MODEQUALS )
    LexKeywordInsert ( "&=", TOKEN_ASSIGN, TOKEN_ANDEQUALS )
    LexKeywordInsert ( "|=", TOKEN_ASSIGN, TOKEN_OREQUALS )
    LexKeywordInsert ( "$=", TOKEN_ASSIGN, TOKEN_XOREQUALS )
    LexKeywordInsert ( "<<=", TOKEN_ASSIGN, TOKEN_LSHEQUALS )
    LexKeywordInsert ( ">>=", TOKEN_ASSIGN, TOKEN_RSHEQUALS )

    // Initialize preprocessor.

    LexPreprocessorInit ()

    // Test some stuff (TEMPORARY)

    //TlSummarizeHashTable ( &LexKeywordHashTable )

    buf : UBYTE[LEX_TOKEN_STRING_SIZE]
    len : ULONG

    tok : LexToken

    LexGetToken (
        &tok, // token
    )

    WHILE tok.Type != TOKEN_EOF DO
        TlPrintNumber ( tok.Type )
        TlPrintString ( "\n" )

        IF tok.Type == TOKEN_STRING THEN
            str := CAST tok.Payload TO ^LexInternedString

            TlPrintString ( str^.DynamicBuffer.Buffer )
            TlPrintString ( "\n" )
        END

        IF tok.Type == TOKEN_NUMBER THEN
            TlPrintString ( "num: " )
            TlPrintNumber ( tok.Payload )
            TlPrintString ( "\n" )
        END

        LexGetToken (
            &tok, // token
        )
    END

//  dynamicbuf : TlDynamicBuffer
//
//  TlInitializeDynamicBuffer ( &dynamicbuf )
//
//  i := 0
//  c := '0'
//
//  WHILE i < 1024 DO
//      IF c > '9' THEN
//          c = '0'
//      END
//
//      TlInsertDynamicBuffer (
//          &dynamicbuf, // array
//          c, // byte
//      )
//
//      c += 1
//      i += 1
//  END
//
//  i = 0
//
//  WHILE i < dynamicbuf.Count DO
//      TlPrintCharacter ( dynamicbuf.Buffer[i] )
//
//      i += 1
//  END
END

FN (TlHashTableEnumeratorF) LexMacroDelete (
    IN entry : ^TlHashTableEntry,
)

    // TODO: free buffer

    // Macros are bump-allocated, so it's meaningless to free one.
    // TlFree ( entry )
END

FN (TlHashTableEnumeratorF) LexSymbolDelete (
    IN entry : ^TlHashTableEntry,
)

    // TODO: free whatever context symbols have

    // Symbols are bump-allocated, so it's meaningless to free one.
    // TlFree ( entry )
END

LexCharacterBackslash := FALSE
LexCharacterInString := FALSE
LexCharacterInLiteral := FALSE
LexCharacterPutback : UBYTE = 0
LexCharacterCoalescePutback : UBYTE = 0

FN (LexGetCharacterF) LexGetCharacter () : UBYTE

    // Return the next character in the source stream. Omits comments and text
    // that is killed by the "preprocessor", and notices preprocessor
    // directives, which it ships off to a special little interpreter.

    byte : UBYTE

    comment := FALSE

    WHILE TRUE DO
        IF LexCharacterCoalescePutback THEN
            byte = LexCharacterCoalescePutback
            LexCharacterCoalescePutback = 0
        ELSEIF LexCharacterPutback THEN
            byte = LexCharacterPutback
            LexCharacterPutback = 0
        ELSE
            byte = LexStreamNextCharacter ()

            IF NOT byte THEN
                // EOF has been reached.

                BREAK
            END
        END

        // We care about backslashes and about knowing whether we're in a
        // string or not in this routine, because otherwise we will
        // mis-identify things as comments or directives that aren't.

        IF comment THEN
            IF byte != '\n' THEN
                CONTINUE
            END

            comment = FALSE

            // Make sure this newline is seen by the higher level code,
            // otherwise a token that is immediately followed by a comment
            // with no interfering whitespace may not terminate properly.

        ELSEIF LexCharacterBackslash THEN
            LexCharacterBackslash = FALSE

        ELSEIF byte == '\\' THEN
            LexCharacterBackslash = TRUE

        ELSEIF LexCharacterInString THEN
            LexCharacterInString = NOT (byte == '\"')

        ELSEIF LexCharacterInLiteral THEN
            LexCharacterInLiteral = NOT (byte == 0x27) // single-quote

        ELSEIF byte == '/' THEN
            // We have to figure out if this is a comment or not. This
            // requires looking at the next character and seeing if it is also
            // a forward slash character. If it is, we set comment to true. If
            // it isn't, we pass this forward slash along and set putback to
            // the value of the character we peeked, causing it to be returned
            // on the next call.

            nextbyte := LexStreamNextCharacter ()

            IF nextbyte == '/' THEN
                comment = TRUE

                CONTINUE
            END

            LexCharacterPutback = nextbyte

        ELSEIF byte == '\"' THEN
            LexCharacterInString = TRUE

        ELSEIF byte == 0x27 THEN // single-quote
            LexCharacterInLiteral = TRUE

        ELSEIF byte == '#' THEN
            // We found a preprocessor directive! We dispatch to
            // the preprocessor regardless of the value of the false count.
            // This gives it an opportunity to manipulate the false count, but
            // it must be sure to ignore directives that aren't relevant.

            LexParseDirective ()

            // The preprocessor consumed the newline character terminating the
            // directive, so we return one here for good measure.

            byte = '\n'
        END

        IF LexFalseCount THEN
            // A non-zero false count causes all characters to be ignored
            // until this situation is resolved by a preprocessor directive.
            // This is used for conditional compilation.

            CONTINUE
        END

        BREAK
    END

    RETURN byte
END

CONST TOKEN_FLAG_SPLIT    := 1
CONST TOKEN_FLAG_COALESCE := 2
CONST TOKEN_FLAG_STRING   := 4
CONST TOKEN_FLAG_CHARLIT  := 8
CONST TOKEN_FLAG_UPPER    := 16

FN LexGetTokenInternal (
    IN token : ^LexToken,
    IN buffer : ^UBYTE,
    OUT length : ^ULONG,
    OUT internedstring : ^^LexInternedString,
) : UBYTE

    // Reads the string of the next token into the provided buffer. Identifies
    // macros and expands them. Returns a set of flags indicating things about
    // the token; for instance, if EOF has been reached (i.e. there is no
    // token), if the token is all uppercase, and if the token is a SPLIT or
    // COALESCE token. The buffer is assumed to be LEX_TOKEN_STRING_SIZE bytes
    // in length or greater.

    length^ = 0

    // Skip leading whitespace.

    stream := LexCurrentStream

    IF stream THEN
        token^.FileBlock = stream^.FileBlock
        token^.LineNumber = stream^.LineNumber
        token^.LinePosition = stream^.LinePosition
    END

    byte := LexGetCharacter ()

    WHILE LexCharTreatment[byte] == CHAR_WHITESPACE DO
        // Re-capture the stream location information for each character of
        // whitespace consumed, until we get the real thing.

        stream = LexCurrentStream

        IF stream THEN
            token^.FileBlock = stream^.FileBlock
            token^.LineNumber = stream^.LineNumber
            token^.LinePosition = stream^.LinePosition
        END

        byte = LexGetCharacter ()
    END

    IF NOT byte THEN
        // EOF encountered.

        RETURN 0
    END

    IF byte == '\"' THEN
        // This is a string token.
        // Bump-allocate a LexInternedString.

        status := TlBumpAlloc (
            SIZEOF LexInternedString, // bytes
            internedstring, // OUT ptr
        )

        IF status THEN
            TlInternalError ( "Failed to allocate interned string", 0, 0, 0 )
        END

        dynamicbuffer := &internedstring^^.DynamicBuffer

        TlInitializeDynamicBuffer ( dynamicbuffer )

        LexGetStringTokenInternal (
            &LexGetCharacter, // getbytefunc
            token, // token
            '\"', // terminator
            dynamicbuffer, // dynamicbuffer
            NULLPTR, // buffer
            NULLPTR, // OUT length
        )

        // Lookup or insert the string in the interned string hash table.

        str := CAST TlLookupOrInsertHashTable (
            &LexInternedStringHashTable, // hashtable
            CAST internedstring^ TO ^TlHashTableEntry, // entry
            dynamicbuffer^.Buffer, // key
        ) TO ^LexInternedString

        IF str != internedstring^ THEN
            // Release our dynamic buffer and return the one we found.
            // I wish we could free the dynamic buffer structure itself, but
            // we already bump allocated it. This is done under the assumption
            // that the most common case is a unique string, and that we aren't
            // likely to find anything in the hash table.

            TlUninitializeDynamicBuffer ( dynamicbuffer )

            internedstring^ = str
        END

        RETURN TOKEN_FLAG_STRING
    END

    IF byte == 0x27 THEN // single-quote
        // This is a character literal.

        LexGetStringTokenInternal (
            &LexGetCharacter, // getbytefunc
            token, // token
            0x27, // terminator
            NULLPTR, // dynamicbuffer
            buffer, // buffer
            length, // OUT length
        )

        RETURN TOKEN_FLAG_CHARLIT
    END

    IF LexCharTreatment[byte] == CHAR_SPLIT THEN
        // This is a split token. It consists of exactly this character and no
        // more.

        length^ = 1
        buffer[0] = byte
        buffer[1] = 0

        RETURN TOKEN_FLAG_SPLIT
    END

    IF LexCharTreatment[byte] == CHAR_COALESCE THEN
        // This is a coalesce token. It consists of this character and the
        // following coalesce characters, and no more.

        len := 0

        WHILE LexCharTreatment[byte] == CHAR_COALESCE DO
            IF len == LEX_TOKEN_STRING_SIZE-1 THEN
                LexTokenError ( token,
                    "Token length exceeds maximum.", 0, 0, 0 )
            END

            buffer[len] = byte
            len += 1

            byte = LexGetCharacter ()
        END

        buffer[len] = 0
        length^ = len

        IF LexCharTreatment[byte] != CHAR_WHITESPACE THEN
            // Set the putback character to the last one we saw, since that is part
            // of the next token.

            LexCharacterCoalescePutback = byte
        END

        RETURN TOKEN_FLAG_COALESCE
    END

    // This is a normal token. All we have to track is whether it consists only
    // of uppercase letters A-Z, in which case it must be a keyword. It could
    // also turn out to be a macro, in which case we must add it to the stream
    // stack and loop here.

    uppercaseonly := byte >= 'A' AND byte <= 'Z'

    len := 0

    WHILE LexCharTreatment[byte] == CHAR_NORMAL DO
        IF len == LEX_TOKEN_STRING_SIZE-1 THEN
            LexTokenError ( token, "Token length exceeds maximum.", 0, 0, 0 )
        END

        IF uppercaseonly THEN
            uppercaseonly = byte >= 'A' AND byte <= 'Z'
        END

        buffer[len] = byte
        len += 1

        byte = LexGetCharacter ()
    END

    buffer[len] = 0
    length^ = len

    IF LexCharTreatment[byte] == CHAR_SPLIT OR
        LexCharTreatment[byte] == CHAR_COALESCE THEN

        LexCharacterCoalescePutback = byte
    END

    IF uppercaseonly AND len > 1 THEN
        RETURN TOKEN_FLAG_UPPER
    END

    // Check if this is a defined macro name. If so, place that macro on the top
    // of the stream stack and loop to the beginning of this procedure.
    //
    // N.B. Jackal macros are "hygienic", meaning that they have no access to
    // the "parent scope", except via their arguments, which become special
    // macros that restore the parent scope during their expansion. This might
    // require associating scopes with streams, so that we know what to restore
    // when we pop a stream off the stream stack.
    //
    // NOTE, also, that we might have consumed the whitespace character after
    // the macro, so if the macro's contents don't conclude with a newline, we
    // might not tokenize that correctly. See if the same trick we use to avoid
    // this case with file streams in LexStreamNextCharacter is applicable to
    // macro streams as well.

    // TODO

    RETURN 0
END

FN LexGetStringTokenInternal (
    IN getbytefunc : LexGetCharacterF,
    IN token : ^LexToken,
    IN terminator : UBYTE,
    IN dynamicbuffer : ^TlDynamicBuffer,
    IN buffer : ^UBYTE,
    OUT length : ^ULONG,
)

    len := 0
    isbackslash := FALSE

    WHILE TRUE DO
        byte := getbytefunc ()

        IF NOT byte THEN
            LexTokenError ( token, "String token terminated by EOF!", 0, 0, 0 )
        END

        IF NOT isbackslash THEN
            IF byte == terminator THEN
                // String is done.

                BREAK
            ELSEIF byte == '\\' THEN
                isbackslash = TRUE

                CONTINUE
            END
        ELSE
            isbackslash = FALSE

            IF byte == 'n' THEN
                byte = '\n'
            
            ELSEIF byte == 't' THEN
                byte = '\t'
            
            ELSEIF byte == 'r' THEN
                byte = '\r'
            
            ELSEIF byte == 'b' THEN
                byte = '\b'
            
            ELSEIF byte == '[' THEN
                byte = '\['
            END
        END

        IF dynamicbuffer THEN
            TlInsertDynamicBuffer ( dynamicbuffer, byte )
        
        ELSEIF len == LEX_TOKEN_STRING_SIZE-1 THEN
            LexTokenError ( token, "Token length exceeds maximum.", 0, 0, 0 )
        
        ELSE
            buffer[len] = byte
            len += 1
        END
    END

    IF dynamicbuffer THEN
        TlInsertDynamicBuffer ( dynamicbuffer, 0 )
    ELSE
        buffer[len] = 0
        length^ = len
    END
END

FN LexGetToken (
    IN token : ^LexToken,
)

    // LexGetTokenInternal might need to allocate an interned string structure.

    internedstring : ^LexInternedString

    buffer : UBYTE[LEX_TOKEN_STRING_SIZE]
    length : ULONG

    tokenflag := LexGetTokenInternal (
        token, // token
        &buffer[0], // buffer
        &length, // OUT length
        &internedstring, // OUT internedstring
    )

    IF tokenflag & ( TOKEN_FLAG_SPLIT | TOKEN_FLAG_COALESCE |
       TOKEN_FLAG_UPPER ) THEN

        // This must be a keyword token. If it doesn't hash to a keyword,
        // there's a syntax error.

        keyword := CAST TlLookupHashTable (
            &LexKeywordHashTable, // hashtable
            &buffer[0], // key
        ) TO ^LexKeyword

        IF NOT keyword THEN
            IF tokenflag & TOKEN_FLAG_UPPER THEN
                LexTokenError ( token,
                    "Bad use of uppercase alphabetic token (must be keyword).",
                    0, 0, 0 )
            END

            LexTokenError ( token, "Unknown keyword", 0, 0, 0 )
        END

        token^.Type = keyword^.Type
        token^.Subtype = keyword^.Subtype

        LEAVE
    END

    IF tokenflag & TOKEN_FLAG_CHARLIT THEN
        // This is a character literal token. We turn those into numerical
        // tokens.

        num := LexCrunchCharacterLiteral ( &buffer[0] )

        token^.Type = TOKEN_NUMBER
        token^.Payload = num

        LEAVE
    END

    IF tokenflag & TOKEN_FLAG_STRING THEN
        // This is a string token. Record the interned string.

        token^.Type = TOKEN_STRING
        token^.Payload = internedstring

        LEAVE
    END

    IF NOT length THEN
        // EOF has been reached.

        token^.Type = TOKEN_EOF

        LEAVE
    END

    IF buffer[0] >= '0' AND buffer[0] <= '9' THEN
        // This is a numerical token.

        num := LexCrunchNumber (
            token, // token
            &buffer[0], // buffer
        )

        token^.Type = TOKEN_NUMBER
        token^.Payload = num

        LEAVE
    END

    // This is an identifier. We have to create an entry in the symbol table
    // for the current scope, if one does not exist.

    token^.Type = TOKEN_IDENTIFIER
    token^.Subtype = TOKEN_IDENTIFIER_FOUND

    symbol := CAST TlLookupSymbolTable (
        LexCurrentScope, // symboltable
        &buffer[0], // name
    ) TO ^LexSymbol

    IF NOT symbol THEN
        // Create new symbol.

        status := TlBumpAlloc (
            SIZEOF LexSymbol + length + 1, // bytes
            &symbol, // OUT ptr
        )

        IF status THEN
            TlInternalError ( "Couldn't allocate symbol", 0, 0, 0 )
        END

        // Place the symbol name directly after the symbol structure in memory.

        symbol^.Name = symbol + SIZEOF LexSymbol

        TlCopyMemory (
            symbol^.Name, // dest
            &buffer[0], // src
            length + 1, // sz (plus one for null terminator)
        )

        TlInsertSymbolTable (
            LexCurrentScope, // symboltable
            CAST symbol TO ^TlHashTableEntry, // entry
            symbol^.Name, // name
        )

        token^.Subtype = TOKEN_IDENTIFIER_NEW
    END

    token^.Payload = symbol
END

FN LexCrunchCharacterLiteral (
    IN buffer : ^UBYTE,
) : ULONG

    num : ULONG = 0
    i := 0

    WHILE buffer[i] DO
        num *= 256
        num += buffer[i]

        i += 1
    END

    RETURN num
END

FN LexCrunchNumber (
    IN token : ^LexToken,
    IN buffer : ^UBYTE,
) : ULONG

    num : ULONG = 0
    i := 0

    IF buffer[0] == '0' THEN
        // This number is either a literal zero, octal, or hexadecimal.

        IF NOT buffer[1] THEN
            // Literal zero.

            RETURN 0
        END

        IF buffer[1] == 'x' THEN
            // Hexadecimal.

            IF NOT buffer[2] THEN
                LexTokenError ( token, "Unfinished hex number.", 0, 0, 0 )
            END

            i = 2

            WHILE buffer[i] DO
                num *= 16

                IF buffer[i] >= '0' AND buffer[i] <= '9' THEN
                    num += buffer[i] - '0'
                
                ELSEIF buffer[i] >= 'A' AND buffer[i] <= 'F' THEN
                    num += buffer[i] - 'A'
                
                ELSEIF buffer[i] >= 'a' AND buffer[i] <= 'f' THEN
                    num += buffer[i] - 'a'

                ELSE
                    LexTokenError ( token, "Malformed hex number.", 0, 0, 0 )
                END

                i += 1
            END

            RETURN num
        END

        // Octal.

        i = 1

        WHILE buffer[i] DO
            IF NOT (buffer[i] >= '0' AND buffer[i] <= '7') THEN
                LexTokenError ( token, "Malformed octal number.", 0, 0, 0 )
            END

            num *= 8
            num += buffer[i] - '0'

            i += 1
        END

        RETURN num
    END

    // Decimal.

    WHILE buffer[i] DO
        IF NOT (buffer[i] >= '0' AND buffer[i] <= '9') THEN
            LexTokenError ( token, "Malformed decimal number.", 0, 0, 0 )
        END

        num *= 10
        num += buffer[i] - '0'

        i += 1
    END

    RETURN num
END