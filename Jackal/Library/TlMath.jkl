//
// Math helper routines.
//

#INCLUDE "<inc>/Runtime.hjk"

FN TlIsPowerOfTwo (
    IN constant : TlMachineWord,
) : ULONG

    // Check if the constant is a power of two. If it is, return which Nth power
    // of two it is.

    IF constant == 0 THEN
        RETURN -1
    END

    bitpos := 0

    WHILE TRUE DO
        IF constant & 1 THEN
            IF constant != 1 THEN
                RETURN -1
            END

            RETURN bitpos
        END

        constant >>= 1
        bitpos += 1
    END
END

FN TlPunchValue (
    IN ptr : ^UBYTE,
    IN value : TlMachineWord,
    IN bytes : ULONG,
)

    // Punch a value into a byte array. Note: always little-endian.

    WHILE bytes DO
        ptr^ = value

        ptr += 1
        value >>= 8
        bytes -= 1
    END
END