//
// Generic filesystems support.
//

#INCLUDE "Dut.hjk"

EXTERN DutAisixFs : DutFilesystem

DutFilesystemTable : ^DutFilesystem[] = {
    &DutAisixFs,
    NULLPTR, // MUST be here, as a sentinel
}

FN DutLookupFilesystem (
    IN fsname : ^UBYTE,
) : ^DutFilesystem

    i := 0
    fs := &DutFilesystemTable[0]

    WHILE fs^ DO
        IF TlCompareString ( fs^^.Name, fsname ) == 0 THEN
            RETURN fs^
        END

        i += 1
        fs += SIZEOF ^VOID
    END

    RETURN NULLPTR
END

FN DutMountFilesystem (
    IN volume : ^DutDiskCommon,
    IN fs : ^DutFilesystem,
) : ^DutMount

    // Allocate a mount block.

    mount : ^DutMount

    status := TlAlloc (
        SIZEOF DutMount, // bytes
        OUT mount, // ptr
    )

    IF status THEN
        TlInternalError ( "Failed to allocate mount block", 0, 0, 0 )
    END

    mount^.Volume = volume
    mount^.Cache = DutCreateCache ( volume )
    mount^.Fs = fs

    // Call the filesystem.

    IF fs^.Mount ( mount ) THEN
        RETURN mount
    END

    DutDeleteCache (
        mount^.Cache, // cache
        FALSE, // doflush
    )

    TlFree ( mount )

    RETURN NULLPTR
END

FN DutUnmountFilesystem (
    IN mount : ^DutMount,
)

    // Call the filesystem.

    IF mount^.Fs^.Unmount THEN
        mount^.Fs^.Unmount ( mount )
    END

    // Flush and destroy the mount cache.

    DutDeleteCache (
        mount^.Cache, // cache
        TRUE, // doflush
    )

    // Deallocate the mount block.

    TlFree ( mount )
END

FN DutOpenFile (
    IN mount : ^DutMount,
    IN path : ^UBYTE,
    IN create : UWORD,
) : ^DutFile

    // Allocate a file block.

    file : ^DutFile

    status := TlAlloc (
        SIZEOF DutFile, // bytes
        OUT file, // ptr
    )

    IF status THEN
        TlInternalError ( "Failed to allocate file block", 0, 0, 0 )
    END

    file^.Mount = mount
    file^.Size = -1

    // Call the filesystem to fill in the file block.

    IF mount^.Fs^.Open (
        file, // file
        path, // path
        create, // create
    ) THEN
        RETURN file
    END

    TlFree ( file )

    RETURN NULLPTR
END