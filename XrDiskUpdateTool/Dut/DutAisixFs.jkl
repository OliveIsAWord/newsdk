//
// AisixFS support.
//

#INCLUDE "Dut.hjk"

#DEFINE AFS_INODE_FILE 1
#DEFINE AFS_INODE_DIR 2

STRUCT AfsSuperblock
    Version : ULONG,
    Magic : ULONG,
    Dirty : ULONG,
    ReservedBlocks : ULONG,
    FatStart : ULONG,
    FatSize : ULONG,
    Istart : ULONG,
    Icount : ULONG,
    DataStart : ULONG,
    DataSize : ULONG,
    VolSize : ULONG,
END

STRUCT AfsDirent
    Inumber : ULONG,
    Name : UBYTE[60],
END

STRUCT AfsInode
    Type : ULONG,
    Permissions : ULONG,
    Uid : ULONG,
    Iparent : ULONG,
    Timestamp : ULONG,
    StartBlock : ULONG,
    Gid : ULONG,
    ByteSize : ULONG,
END

FN AfsInitializeInodeContext (
    IN icontext : ^AfsInodeContext,
    IN inum : UWORD,
    IN mount : ^DutMount,
)

    bn := (inum * SIZEOF AfsInode) / 512 + mount^.Context.Afs.Istart

    itable : ^AfsInode

    icontext^.Iblock = DutReadCache (
        mount^.Cache, // cache
        bn, // lba
        OUT itable, // data
    )

    icontext^.Inode = &itable[inum % (512 / SIZEOF AfsInode)]

    icontext^.Mount = mount
    icontext^.LookupCache = NULLPTR
    icontext^.LookupCacheSize = 0
END

FN AfsUninitializeInodeContext (
    IN icontext : ^AfsInodeContext,
)

    DutReleaseBlock ( icontext^.Iblock )

    IF icontext^.LookupCache THEN
        TlFree ( icontext^.LookupCache )
    END
END

FN AfsSetBlockStatus (
    IN cache : ^DutCache,
    IN block : UWORD,
    IN fatstart : UWORD,
    IN status : ULONG,
)

    // Set the FAT entry for the specified block.

    bn := (block / 128) + fatstart

    data : ^ULONG

    fatblock := DutReadCache (
        cache, // cache
        bn, // lba
        OUT data, // data
    )

    data[block % 128] = status

    DutDirtyBlock ( fatblock )

    DutReleaseBlock ( fatblock )
END

FN AfsGetBlockStatus (
    IN cache : ^DutCache,
    IN block : UWORD,
    IN fatstart : UWORD,
) : ULONG

    // Get the FAT entry for the specified block.

    bn := (block / 128) + fatstart

    data : ^ULONG

    fatblock := DutReadCache (
        cache, // cache
        bn, // lba
        OUT data, // data
    )

    status := data[block % 128]

    DutReleaseBlock ( fatblock )

    RETURN status
END

FN AfsAllocateBlock (
    IN mount : ^DutMount,
) : UWORD

    j := 0

    WHILE j < 2 DO
        i : UWORD
        max : UWORD

        IF j == 0 THEN
            i = mount^.Context.Afs.AllocHint
            max = mount^.Volume^.SectorCount

        ELSEIF mount^.Context.Afs.AllocHint THEN
            i = 0
            max = mount^.Context.Afs.AllocHint

        ELSE
            BREAK
        END

        WHILE i < max DO
            status := AfsGetBlockStatus (
                mount^.Cache, // cache
                i, // block
                mount^.Context.Afs.FatStart, // fatstart
            )

            IF status == 0 THEN
                mount^.Context.Afs.AllocHint = i + 1

                AfsSetBlockStatus (
                    mount^.Cache, // cache
                    i, // block
                    mount^.Context.Afs.FatStart, // fatstart
                    0xFFFFFFFF, // status
                )

                // Zero out the block.

                data : ^UBYTE

                block := DutReadCache (
                    mount^.Cache, // cache
                    i, // lba
                    OUT data, // data
                )

                TlFillMemoryWithByte (
                    data, // ptr
                    512, // sz
                    0, // byte
                )

                DutDirtyBlock ( block )

                DutReleaseBlock ( block )

                RETURN i
            END

            i += 1
        END

        j += 1
    END

    RETURN 0xFFFFFFFF
END

FN AfsFreeChain (
    IN mount : ^DutMount,
    IN block : UWORD,
)

    WHILE block != 0xFFFFFFFF DO
        nextblock := AfsGetBlockStatus (
            mount^.Cache, // cache
            block, // block
            mount^.Context.Afs.FatStart, // fatstart
        )

        AfsSetBlockStatus (
            mount^.Cache, // cache
            block, // block
            mount^.Context.Afs.FatStart, // fatstart
            0, // status
        )

        block = nextblock
    END
END

FN AfsTruncateInode (
    IN icontext : ^AfsInodeContext,
) : UWORD

    AfsFreeChain (
        icontext^.Mount, // mount
        icontext^.Inode^.StartBlock, // block
    )

    icontext^.Inode^.StartBlock = 0xFFFFFFFF
    icontext^.Inode^.ByteSize = 0

    DutDirtyBlock ( icontext^.Iblock )
END

FN AfsDeleteInode (
    IN icontext : ^AfsInodeContext,
) : UWORD

    AfsFreeChain (
        icontext^.Mount, // mount
        icontext^.Inode^.StartBlock, // block
    )

    icontext^.Inode^.Type = 0

    DutDirtyBlock ( icontext^.Iblock )
END

FN AfsSetLookupCacheEntry (
    IN icontext : ^AfsInodeContext,
    IN bn : UWORD,
    IN lba : UWORD,
)

    // Set the specified block number of the lookup cache with the given LBA.

    size := icontext^.LookupCacheSize

    IF bn < size THEN
        icontext^.LookupCache[bn] = lba

        LEAVE
    END

    // Have to extend it.

    oldsize := size

    IF size == 0 THEN
        size = 8
    END

    WHILE bn >= size DO
        size *= 2
    END

    newptr : ^ULONG

    status := TlAlloc (
        size * SIZEOF ULONG, // bytes
        OUT newptr, // ptr
    )

    IF status THEN
        TlInternalError ( "Failed to extend lookup cache", size, 0, 0 )
    END

    IF icontext^.LookupCache THEN
        // Copy old stuff into the new block.

        TlCopyMemory (
            newptr, // dest
            icontext^.LookupCache, // src
            oldsize * SIZEOF ULONG, // sz
        )
    END

    // Free old cache.

    TlFree ( icontext^.LookupCache )

    // Zero out new length.

    TlFillMemoryWithByte (
        newptr + oldsize * SIZEOF ULONG, // dest
        (size - oldsize) * SIZEOF ULONG, // sz
        0, // byte
    )

    // Set block number.

    newptr[bn] = lba

    // Set new stuff.

    icontext^.LookupCache = newptr
    icontext^.LookupCacheSize = size
END

FN AfsGetBlock (
    IN icontext : ^AfsInodeContext,
    IN bn : UWORD,
    IN writing : UWORD,
) : UWORD

    // Translate a block number offset within a file to its LBA on disk.

    cache := icontext^.LookupCache
    size := icontext^.LookupCacheSize

    IF bn < size AND cache[bn] THEN
        RETURN cache[bn]
    END

    lba : UWORD
    prevblock : UWORD
    index := bn

    IF size THEN
        // Start from the last cached block.

        IF index >= size THEN
            index = size - 1
        END

        WHILE NOT cache[index] AND index DO
            index -= 1
        END

    ELSE
        index = 0
    END

    IF index == 0 THEN
        // We have to start from the start block.

        lba = icontext^.Inode^.StartBlock

        IF lba == 0xFFFFFFFF THEN
            // Allocate start block.

            IF NOT writing THEN
                TlInternalError ( "balloc on read 1", 0, 0, 0 )
            END

            lba = AfsAllocateBlock ( icontext^.Mount )

            IF lba == 0xFFFFFFFF THEN
                // Out of disk space.

                RETURN -1
            END

            IF lba == 0 THEN
                TlInternalError ( "Allocated the superblock", 0, 0, 0 )
            END

            // Set start block in inode.

            icontext^.Inode^.StartBlock = lba

            DutDirtyBlock ( icontext^.Iblock )
        END
    
        AfsSetLookupCacheEntry (
            icontext, // icontext
            0, // bn
            lba, // lba
        )

        cache = icontext^.LookupCache
    END

    lba = cache[index]
    index += 1

    WHILE index <= bn DO
        prevblock = lba

        lba = AfsGetBlockStatus (
            icontext^.Mount^.Cache, // cache
            lba, // block
            icontext^.Mount^.Context.Afs.FatStart, // fatstart
        )

        IF lba == 0xFFFFFFFF THEN
            IF NOT writing THEN
                TlInternalError ( "balloc on read 2", lba, index, bn )
            END

            lba = AfsAllocateBlock ( icontext^.Mount )

            IF lba == 0xFFFFFFFF THEN
                // Out of disk space.

                RETURN -1
            END

            IF lba == 0 THEN
                TlInternalError ( "Allocated the superblock", 1, 0, 0 )
            END

            // Set link on previous block.

            AfsSetBlockStatus (
                icontext^.Mount^.Cache, // cache
                prevblock, // block
                icontext^.Mount^.Context.Afs.FatStart, // fatstart
                lba, // status
            )
        END

        AfsSetLookupCacheEntry (
            icontext, // icontext
            index, // bn
            lba, // lba
        )

        index += 1
    END

    RETURN lba
END

FN AfsReadInode (
    IN icontext : ^AfsInodeContext,
    IN offset : UWORD,
    IN len : UWORD,
    IN buf : ^VOID,
) : UWORD

    inode := icontext^.Inode

    IF offset >= inode^.ByteSize THEN
        RETURN 0
    END

    IF offset + len > inode^.ByteSize THEN
        len = inode^.ByteSize - offset
    END

    count := 0

    WHILE count < len DO
        lba := AfsGetBlock (
            icontext, // context
            offset / 512, // bn
            FALSE, // writing
        )

        IF lba == 0xFFFFFFFF THEN
            // Failed to get next block.

            RETURN 0xFFFFFFFF
        END

        amt := 512 - (offset % 512)

        IF amt > len - count THEN
            amt = len - count
        END

        data : ^UBYTE

        block := DutReadCache (
            icontext^.Mount^.Cache, // cache
            lba, // lba
            OUT data, // data
        )

        TlCopyMemory (
            buf, // dest
            &data[offset % 512], // src
            amt, // sz
        )

        DutReleaseBlock ( block )

        offset += amt
        count += amt
        buf += amt
    END

    RETURN len
END

FN AfsWriteInode (
    IN icontext : ^AfsInodeContext,
    IN offset : UWORD,
    IN len : UWORD,
    IN buf : ^VOID,
) : UWORD

    inode := icontext^.Inode

    count := 0

    WHILE count < len DO
        lba := AfsGetBlock (
            icontext, // context
            offset / 512, // bn
            TRUE, // writing
        )

        IF lba == 0xFFFFFFFF THEN
            // Failed to get next block.

            RETURN 0xFFFFFFFF
        END

        amt := 512 - (offset % 512)

        IF amt > len - count THEN
            amt = len - count
        END

        data : ^UBYTE

        block := DutReadCache (
            icontext^.Mount^.Cache, // cache
            lba, // lba
            OUT data, // data
        )

        TlCopyMemory (
            &data[offset % 512], // dest
            buf, // src
            amt, // sz
        )

        DutDirtyBlock ( block )

        DutReleaseBlock ( block )

        offset += amt
        count += amt
        buf += amt
    END

    IF offset > inode^.ByteSize THEN
        inode^.ByteSize = offset

        DutDirtyBlock ( icontext^.Iblock )
    END

    RETURN len
END

FN AfsAllocateInode (
    IN mount : ^DutMount,
    OUT inode : ^AfsInode,
    OUT iblock : ^DutCacheBlock,
) : UWORD

    // Allocate an inode and return the number, or -1 if not found.

    j := 0
    i : UWORD
    max : UWORD

    WHILE j < 2 DO
        IF j == 0 THEN
            i = mount^.Context.Afs.Ihint
            max = mount^.Context.Afs.Icount

            IF i == 0 THEN
                i = 1
            END

        ELSEIF mount^.Context.Afs.Ihint THEN
            i = 1
            max = mount^.Context.Afs.Ihint

        ELSE
            BREAK
        END

        curbn := -1
        itable : ^AfsInode

        WHILE i < max DO
            bn := i / (512 / SIZEOF AfsInode)

            IF curbn != bn THEN
                IF curbn != -1 THEN
                    DutReleaseBlock ( iblock )
                END

                iblock = DutReadCache (
                    mount^.Cache, // cache
                    mount^.Context.Afs.Istart + bn, // lba
                    OUT itable, // data
                )

                curbn = bn
            END

            inode = &itable[i % (512 / SIZEOF AfsInode)]

            IF inode^.Type == 0 THEN
                // Free! Take it. Leave iblock referenced.

                mount^.Context.Afs.Ihint = i + 1

                RETURN i
            END

            i += 1
        END

        IF curbn != -1 THEN
            DutReleaseBlock ( iblock )
        END

        j += 1
    END

    RETURN -1
END

FN AfsCreateDirectoryEntry (
    IN icontext : ^AfsInodeContext,
) : UWORD

    // Allocate a directory entry. Return the offset within the directory.

    i := 0
    max := icontext^.Inode^.ByteSize / SIZEOF AfsDirent
    dirent : AfsDirent

    WHILE i < max DO
        AfsReadInode (
            icontext, // icontext
            i * SIZEOF AfsDirent, // offset
            SIZEOF AfsDirent, // len
            &dirent, // buf
        )

        IF dirent.Inumber == 0 THEN
            // Free! Take it.

            RETURN i * SIZEOF AfsDirent
        END

        i += 1
    END

    // Extend the directory.

    dirent.Inumber = 0

    len := AfsWriteInode (
        icontext, // icontext
        max * SIZEOF AfsDirent, // offset
        SIZEOF AfsDirent, // len
        &dirent, // buf
    )

    IF len == -1 THEN
        RETURN -1
    END

    RETURN max * SIZEOF AfsDirent
END

FN AfsEnsureEmpty (
    IN icontext : ^AfsInodeContext,
) : UWORD

    dirent : AfsDirent
    offset := 0

    WHILE TRUE DO
        len := AfsReadInode (
            icontext, // icontext
            offset, // offset
            SIZEOF AfsDirent, // len
            &dirent, // buf
        )

        IF len < SIZEOF AfsDirent THEN
            BREAK
        END

        IF dirent.Inumber THEN
            RETURN FALSE
        END

        offset += SIZEOF AfsDirent
    END

    RETURN TRUE
END

FN (DutFilesystemFormatF) DutAisixFsFormat (
    IN volume : ^DutDiskCommon,
)

    IF volume^.SectorLog != 9 THEN
        TlError ( "AisixFS only works with 512 byte blocks\n" )
    END

    cache := DutCreateCache ( volume )

    // Initialize the superblock.

    sb : ^AfsSuperblock

    block := DutReadCache (
        cache, // cache
        0, // lba
        OUT sb, // data
    )

    // Calculate some parameters.

    reservedblocks := 8 // includes superblock
    fatsize := ((volume^.SectorCount * 4) + 511) / 512
    fatstart := reservedblocks
    istart := fatstart + fatsize
    icount := (volume^.SectorCount + 31) / 32
    isize := ((icount * SIZEOF AfsInode) + 511) / 512
    datastart := istart + isize

    sb^.Magic = 0xAFBBAFBB
    sb^.Version = 6
    sb^.ReservedBlocks = reservedblocks
    sb^.FatStart = fatstart
    sb^.FatSize = fatsize
    sb^.Istart = istart
    sb^.Icount = icount
    sb^.DataStart = datastart
    sb^.DataSize = volume^.SectorCount - datastart
    sb^.VolSize = volume^.SectorCount

    DutDirtyBlock ( block )

    DutReleaseBlock ( block )

    // Zero the FAT.

    i := 0

    WHILE i < fatsize DO
        DutWriteSector (
            volume, // volume
            &DutZeroBuffer[0], // buf
            fatstart + i, // sector
        )

        i += 1
    END

    // Zero the inode list.

    i = 0

    WHILE i < isize DO
        DutWriteSector (
            volume, // volume
            &DutZeroBuffer[0], // buf
            istart + i, // sector
        )

        i += 1
    END

    // Mark boot blocks reserved.

    i = 0

    WHILE i < reservedblocks DO
        AfsSetBlockStatus (
            cache, // cache
            i, // block
            fatstart, // fatstart
            0xFFFFFFFF, // status
        )

        i += 1
    END

    // Mark FAT reserved.

    i = 0

    WHILE i < fatsize DO
        AfsSetBlockStatus (
            cache, // cache
            fatstart + i, // block
            fatstart, // fatstart
            0xFFFFFFFF, // status
        )

        i += 1
    END

    // Mark inode list reserved.

    i = 0

    WHILE i < isize DO
        AfsSetBlockStatus (
            cache, // cache
            istart + i, // block
            fatstart, // fatstart
            0xFFFFFFFF, // status
        )

        i += 1
    END

    // Hand-craft the root directory inode.

    inode : ^AfsInode

    block = DutReadCache (
        cache, // cache
        istart, // lba
        OUT inode, // data
    )

    inode[1].Uid = 0
    inode[1].Gid = 0
    inode[1].Permissions = 0755
    inode[1].Iparent = 1
    inode[1].Type = AFS_INODE_DIR
    inode[1].Timestamp = TlCurrentEpochTime ()
    inode[1].ByteSize = 0
    inode[1].StartBlock = 0xFFFFFFFF

    DutDirtyBlock ( block )

    DutReleaseBlock ( block )

    // Delete cache and flush changes.

    DutDeleteCache (
        cache, // cache
        TRUE, // doflush
    )
END

FN (DutFilesystemMountF) DutAisixFsMount (
    IN mount : ^DutMount,
) : UWORD

    // Mount the volume.

    // Basically we should read the superblock, validate it, and fill in our
    // member of the mount structure's context union with relevant fields from
    // it. For AisixFS this is *really* simple.

    sb : ^AfsSuperblock

    block := DutReadCache (
        mount^.Cache, // cache
        0, // lba
        OUT sb, // data
    )

    IF sb^.Magic != 0xAFBBAFBB THEN
        RETURN FALSE
    END

    IF sb^.Version != 6 THEN
        RETURN FALSE
    END

    mount^.Context.Afs.FatStart = sb^.FatStart
    mount^.Context.Afs.Istart = sb^.Istart
    mount^.Context.Afs.Icount = (sb^.Icount * 512) / SIZEOF AfsInode
    mount^.Context.Afs.AllocHint = 0
    mount^.Context.Afs.Ihint = 0

    DutReleaseBlock ( block )

    RETURN TRUE
END

FN (DutFilesystemOpenF) DutAisixFsOpen (
    IN file : ^DutFile,
    IN path : ^UBYTE,
    IN create : UWORD,
    OUT error : ^UBYTE,
) : UWORD

    // Resolve the path and fill in the file block so that it can be used for
    // future operations. Return TRUE upon success and FALSE otherwise.
    // If create is TRUE, create all directories along the path if needed, and
    // create the file itself.

    pcomp : UBYTE[60]
    inum := 1
    dirent : AfsDirent
    icontext : AfsInodeContext
    error = NULLPTR
    isdir := FALSE

    WHILE TRUE DO
        // Grab a path component.

        isdir = FALSE

        len := 0

        WHILE path[0] == '/' DO
            path += 1
        END

        WHILE path[0] AND path[0] != '/' DO
            IF len == 59 THEN
                error = "Path component too long"

                RETURN FALSE
            END

            pcomp[len] = path[0]
            path += 1
            len += 1
        END

        IF path[0] == '/' THEN
            isdir = TRUE
        END

        pcomp[len] = 0

        IF len == 0 THEN
            // No more path.

            BREAK
        END

        // Initialize an inode context for this directory.

        AfsInitializeInodeContext (
            &icontext, // icontext
            inum, // inum
            file^.Mount, // mount
        )

        IF icontext.Inode^.Type != AFS_INODE_DIR THEN
            error = "Not a directory"

            AfsUninitializeInodeContext ( &icontext )

            RETURN FALSE
        END

        offset := 0

        WHILE TRUE DO
            // Read next dirent.

            readlen := AfsReadInode (
                &icontext, // icontext
                offset, // offset
                SIZEOF AfsDirent, // len
                &dirent, // buf
            )

            IF readlen == 0xFFFFFFFF THEN
                // Failed to read.

                error = "I/O error on dirent"

                AfsUninitializeInodeContext ( &icontext )

                RETURN FALSE
            END

            IF readlen < SIZEOF AfsDirent THEN
                // Got to the end and didn't find it.

                IF NOT create THEN
                    error = "File not found"

                    AfsUninitializeInodeContext ( &icontext )

                    RETURN FALSE
                END

                // Create the file. First allocate an inode.

                inode : ^AfsInode
                iblock : ^DutCacheBlock

                newinum := AfsAllocateInode (
                    file^.Mount, // mount
                    OUT inode, // inode
                    OUT iblock, // iblock
                )

                IF newinum == -1 THEN
                    error = "No more inodes"

                    AfsUninitializeInodeContext ( &icontext )

                    RETURN FALSE
                END

                // Allocate a directory entry.

                diroff := AfsCreateDirectoryEntry ( &icontext )

                IF diroff == -1 THEN
                    error = "Failed to create dirent"

                    AfsUninitializeInodeContext ( &icontext )

                    RETURN FALSE
                END

                // Set up the directory entry.

                TlFillMemoryWithByte (
                    &dirent, // ptr
                    SIZEOF AfsDirent, // sz
                    0, // byte
                )

                dirent.Inumber = newinum

                TlCopyString (
                    &dirent.Name[0], // dest
                    &pcomp[0], // src
                    60, // bufsize
                )

                // Write it to the directory.

                AfsWriteInode (
                    &icontext, // icontext
                    diroff, // offset
                    SIZEOF AfsDirent, // len
                    &dirent, // buf
                )

                // Set up the inode.

                IF isdir THEN
                    inode^.Type = AFS_INODE_DIR
                    inode^.Permissions = 0755

                ELSE
                    inode^.Type = AFS_INODE_FILE

                    ext := TlGetPathExtension ( &pcomp[0] )

                    IF TlCompareString ( ext, "exe" ) == 0 OR
                        TlCompareString ( ext, "sys" ) == 0 OR
                        TlCompareString ( ext, "dll" ) == 0 THEN

                        // It's an executable.

                        inode^.Permissions = 0755

                    ELSE
                        inode^.Permissions = 0644
                    END
                END

                inode^.Uid = 0
                inode^.Iparent = inum
                inode^.Timestamp = TlCurrentEpochTime ()
                inode^.StartBlock = 0xFFFFFFFF
                inode^.Gid = 0
                inode^.ByteSize = 0

                DutDirtyBlock ( iblock )

                DutReleaseBlock ( iblock )

                inum = newinum

                BREAK

            ELSEIF dirent.Inumber AND TlCompareStringWithMax (
                &pcomp[0], // str1
                &dirent.Name[0], // str2
                60, // count
            ) == 0 THEN

                // Found it.

                inum = dirent.Inumber

                BREAK
            END

            offset += SIZEOF AfsDirent
        END

        AfsUninitializeInodeContext ( &icontext )
    END

    // We now have the inum of the file.

    AfsInitializeInodeContext (
        &file^.Context.Afs, // icontext
        inum, // inum
        file^.Mount, // mount
    )

    RETURN TRUE
END

FN (DutFilesystemCloseF) DutAisixFsClose (
    IN file : ^DutFile,
)

    AfsUninitializeInodeContext ( &file^.Context.Afs )
END

FN (DutFilesystemReadF) DutAisixFsRead (
    IN file : ^DutFile,
    IN offset : UWORD,
    IN len : UWORD,
    IN buf : ^VOID,
) : UWORD

    status := AfsReadInode (
        &file^.Context.Afs, // icontext
        offset, // offset
        len, // len
        buf, // buf
    )

    IF status == 0xFFFFFFFF THEN
        RETURN -1
    END

    RETURN status
END

FN (DutFilesystemWriteF) DutAisixFsWrite (
    IN file : ^DutFile,
    IN offset : UWORD,
    IN len : UWORD,
    IN buf : ^VOID,
) : UWORD

    status := AfsWriteInode (
        &file^.Context.Afs, // icontext
        offset, // offset
        len, // len
        buf, // buf
    )

    IF status == 0xFFFFFFFF THEN
        RETURN -1
    END

    RETURN status
END

FN (DutFilesystemListF) DutAisixFsList (
    IN file : ^DutFile,
) : UWORD

    dirent : AfsDirent
    offset := 0

    WHILE TRUE DO
        len := AfsReadInode (
            &file^.Context.Afs, // icontext
            offset, // offset
            SIZEOF AfsDirent, // len
            &dirent, // buf
        )

        IF len < SIZEOF AfsDirent THEN
            BREAK
        END

        IF dirent.Inumber THEN
            TlPrint ( "    %s\n", &dirent.Name[0] )
        END

        offset += SIZEOF AfsDirent
    END
END

FN (DutFilesystemListF) DutAisixFsTruncate (
    IN file : ^DutFile,
) : UWORD

    AfsTruncateInode ( &file^.Context.Afs )
END

FN (DutFilesystemSetPermissionsF) DutAisixFsSetPermissions (
    IN file : ^DutFile,
    IN permissions : UWORD,
    IN uid : UWORD,
    IN gid : UWORD,
)

    IF permissions != -1 THEN
        file^.Context.Afs.Inode^.Permissions = permissions
    END

    IF uid != -1 THEN
        file^.Context.Afs.Inode^.Uid = uid
    END

    IF gid != -1 THEN
        file^.Context.Afs.Inode^.Gid = gid
    END

    DutDirtyBlock ( file^.Context.Afs.Iblock )
END

FN (DutFilesystemDeleteF) DutAisixFsDelete (
    IN mount : ^DutMount,
    IN path : ^UBYTE,
    OUT error : ^UBYTE,
) : UWORD

    // We have to delete the dirent, delete the inode, and free the blocks.

    pcomp : UBYTE[60]
    inum := 1
    dirent : AfsDirent
    icontext : AfsInodeContext
    error = NULLPTR
    isdir := FALSE
    diroff := -1
    uninit := FALSE

    WHILE TRUE DO
        // Grab a path component.

        isdir = FALSE

        len := 0

        WHILE path[0] == '/' DO
            path += 1
        END

        WHILE path[0] AND path[0] != '/' DO
            IF len == 59 THEN
                error = "Path component too long"

                IF uninit THEN
                    AfsUninitializeInodeContext ( &icontext )
                END

                RETURN FALSE
            END

            pcomp[len] = path[0]
            path += 1
            len += 1
        END

        IF path[0] == '/' THEN
            isdir = TRUE
        END

        pcomp[len] = 0

        IF len == 0 THEN
            // No more path.

            BREAK
        END

        // Initialize an inode context for this directory.

        IF uninit THEN
            AfsUninitializeInodeContext ( &icontext )
        END

        uninit = TRUE

        AfsInitializeInodeContext (
            &icontext, // icontext
            inum, // inum
            mount, // mount
        )

        IF icontext.Inode^.Type != AFS_INODE_DIR THEN
            error = "Not a directory"

            AfsUninitializeInodeContext ( &icontext )

            RETURN FALSE
        END

        offset := 0

        WHILE TRUE DO
            // Read next dirent.

            readlen := AfsReadInode (
                &icontext, // icontext
                offset, // offset
                SIZEOF AfsDirent, // len
                &dirent, // buf
            )

            IF readlen == 0xFFFFFFFF THEN
                // Failed to read.

                error = "I/O error on dirent"

                AfsUninitializeInodeContext ( &icontext )

                RETURN FALSE
            END

            IF readlen < SIZEOF AfsDirent THEN
                // Got to the end and didn't find it.

                error = "File not found"

                AfsUninitializeInodeContext ( &icontext )

                RETURN FALSE

            ELSEIF dirent.Inumber AND TlCompareStringWithMax (
                &pcomp[0], // str1
                &dirent.Name[0], // str2
                60, // count
            ) == 0 THEN

                // Found it.

                inum = dirent.Inumber
                diroff = offset

                BREAK
            END

            offset += SIZEOF AfsDirent
        END
    END

    IF diroff == -1 THEN
        error = "Can't delete root"

        IF uninit THEN
            AfsUninitializeInodeContext ( &icontext )
        END

        RETURN FALSE
    END

    icontext2 : AfsInodeContext

    AfsInitializeInodeContext (
        &icontext2, // icontext
        inum, // inum
        mount, // mount
    )

    IF icontext2.Inode^.Type == AFS_INODE_DIR THEN
        // Make sure it's empty.

        IF NOT AfsEnsureEmpty ( &icontext2 ) THEN
            error = "Directory not empty"

            AfsUninitializeInodeContext ( &icontext )

            AfsUninitializeInodeContext ( &icontext2 )

            RETURN FALSE
        END
    END

    // Free the dirent.

    dirent.Inumber = 0

    AfsWriteInode (
        &icontext, // icontext
        diroff, // offset
        SIZEOF AfsDirent, // len
        &dirent, // buf
    )

    AfsUninitializeInodeContext ( &icontext )

    // Free the inode.

    AfsDeleteInode ( &icontext2 )

    AfsUninitializeInodeContext ( &icontext2 )

    RETURN TRUE
END

PUBLIC DutAisixFs : DutFilesystem = {
    [Name] = "aisixfs",
    [Format] = &DutAisixFsFormat,
    [Mount] = &DutAisixFsMount,
    [Unmount] = NULLPTR,
    [Open] = &DutAisixFsOpen,
    [Close] = &DutAisixFsClose,
    [Read] = &DutAisixFsRead,
    [Write] = &DutAisixFsWrite,
    [List] = &DutAisixFsList,
    [Truncate] = &DutAisixFsTruncate,
    [Delete] = &DutAisixFsDelete,
    [SetPermissions] = &DutAisixFsSetPermissions,
}