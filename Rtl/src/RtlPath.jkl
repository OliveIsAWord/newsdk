//
// Various path manipulation functions.
//

#INCLUDE "<inc>/Rtl.hjk"

EXPORT FN RtlGetPathExtension (
    IN path : ^UBYTE,
) : ^UBYTE

    // Return the extension of the file pointed to by the path. Note that the
    // pointer returned is just a pointer within the path string. If there is no
    // extension, a zero length string is returned.

    len := RtlMeasureString ( path )

    IF len == 0 THEN
        RETURN ""
    END

    len -= 1

    WHILE len AND path[len] != '/' DO
        IF path[len] == '.' AND path[len - 1] != '/' THEN
            RETURN &path[len + 1]
        END

        len -= 1
    END

    RETURN ""
END

EXPORT FN RtlGetFileName (
    IN path : ^UBYTE,
) : ^UBYTE

    // Return a pointer to the last component of the path.

    lcomp := path

    ins := FALSE

    WHILE path^ DO
        IF path^ == '/' THEN
            ins = TRUE

        ELSEIF ins THEN
            ins = FALSE
            lcomp = path
        END

        path += 1
    END

    RETURN lcomp
END